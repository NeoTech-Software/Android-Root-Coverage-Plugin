// Copyright (C) 2018 Rolf Smit
//
// Version 1
//
// This file can be used as a hack to make it possible to use variable/dynamic version numbers for
// plugins in the new Gradle Plugins Block:
// https://docs.gradle.org/current/userguide/plugins.html#sec:plugins_block
// Normally variables cannot be used, but through the project settings.pluginManagement DSL some
// configuration is possible.
//
// How to use?
//
// 1. Add the following to your settings.gradle file:
//
// pluginManagement {
//     apply from: "path/to/plugin-version-handler.gradle", to: pluginManagement
// }
//
// 2. Add the plugins that you want to use variable/dynamic version for to the dependencies.gradle
// file:
//
// ext {
//     projectVersion = [
//             kotlin      : "1.3.0",
//     ]
//
//     pluginVersions = [
//             "org.jetbrains.kotlin.android": "${projectVersion.kotlin}"
//     ]
// }


apply from: "dependencies.gradle"

resolutionStrategy {
    eachPlugin {
        // pluginVersions must be added in dependencies.gradle as extension to this project.
        def version = pluginVersions[requested.id.id]
        if (requested.version == null && version != null) {
            useVersion version
        } else if(requested.version == null) {
            logger.log(LogLevel.WARN, "Plugin with id `${requested.id.id}` is requested without version and no version can be found in the `pluginVersions` map!")
        } else {
            logger.log(LogLevel.WARN, "Plugin with id `${requested.id.id}` is not defined in the `pluginVersions` map, using version ${requested.version}")
        }
    }
}
