import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id "com.android.library"
    // Kotlin cannot be applied here again, as the plugin is already on the class path via withPluginClasspath()
}

android {

    namespace "org.neotech.library.flavors"
    compileSdkVersion libs.versions.androidCompileSdk.get().toInteger()

    defaultConfig {
        minSdkVersion libs.versions.androidMinSdk.get().toInteger()
        targetSdkVersion libs.versions.androidTargetSdk.get().toInteger()
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig false
    }

    buildTypes {
        debug {

            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ["version"]
    productFlavors {
        demo {
            dimension "version"
        }
        full {
            dimension "version"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    testOptions {
        managedDevices {
            localDevices {
                smallphoneapi32 {
                    device = "Small Phone"
                    apiLevel = 32
                    systemImageSource = "aosp"
                }
            }
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_21)
    }
}

dependencies {
    implementation libs.appCompat

    testImplementation libs.bundles.androidTest
    androidTestImplementation libs.bundles.androidInstrumentedTest
}
