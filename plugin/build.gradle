plugins {
    id "org.jetbrains.kotlin.jvm"
    id "java-gradle-plugin"

    // For publishing to Plugin Portal
    id "com.gradle.plugin-publish" version "0.10.0"

    // Bintray & Maven for publishing
    id "com.github.dcendents.android-maven" version "2.1"
    id "com.jfrog.bintray" version "1.8.4"
}

// To upload new plugin artifact to maven and Gradle Portal:
// > gradlew bintrayUpload publishPlugins
//
// To test the plugin:
// > gradlew clean test

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

gradlePlugin {
    plugins {
        rootCoverage {
            id = 'org.neotech.plugin.rootcoverage'
            implementationClass = 'org.neotech.plugin.rootcoverage.RootCoveragePlugin'
        }
    }
}

pluginBundle {
    setWebsite(PROJECT_WEBSITE)
    vcsUrl = 'https://github.com/NeoTech-Software/Android-Root-Coverage-Plugin'
    setDescription(DESCRIPTION)
    tags = ['android', 'coverage', 'jacoco', 'code-coverage', 'coverage-report', 'aggregated-report']

    plugins {
        rootCoverage {
            displayName = 'Android-Root-Coverage-Plugin'
        }
    }

    mavenCoordinates {
        groupId = GROUP
        artifactId = ARTIFACT_ID
        version = VERSION
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams true
    }
}

dependencies {
    compileOnly gradleApi()
    implementation projectDependency.gradlePlugin
    implementation projectDependency.kotlinStdlibJdk7

    testImplementation projectDependency.kotlinStdlibJdk7
    testImplementation projectDependency.kotlinTest
    testImplementation gradleTestKit()
    testImplementation projectDependency.junit
    testImplementation projectDependency.truth
    testImplementation projectDependency.commonsCsv
}

apply from: rootProject.file('publish.gradle')